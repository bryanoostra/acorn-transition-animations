buildscript {
    ext.kotlinVersion = "1.4.21"

    repositories {
        google()
        mavenCentral()
    }

    configurations.maybeCreate("pitest")
    dependencies {
        classpath('com.android.tools.build:gradle:3.5.4')
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

plugins {
    id("com.github.ben-manes.versions") version "0.25.0"
    id("io.spring.dependency-management") version "1.0.8.RELEASE"
}

allprojects {
    apply from: "$rootDir/.ops/git.gradle"
}

subprojects {
    repositories {
        google()
        mavenCentral()
    }

    project.apply from: "$rootDir/dependencies.gradle"

    afterEvaluate { project ->
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
            kotlinOptions {
                freeCompilerArgs += ["-progressive"]
            }
        }

        if (plugins.hasPlugin("com.android.library") || plugins.hasPlugin("com.android.application")) {
            android {
                compileSdkVersion(30)
                defaultConfig {
                    targetSdkVersion(30)

                    versionName = git.versionName()
                    versionCode = git.commitCount()

                    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
                }

                testOptions {
                    unitTests.returnDefaultValues = true
                }

                lintOptions {
                    warningsAsErrors = true
                    textReport = true

                    ignoreTestSources true

                    ignore "ObsoleteLintCustomCheck", "GradleDependency"
                }

                compileOptions {
                    sourceCompatibility 1.8
                    targetCompatibility 1.8
                }
            }
        }

        if (project.hasProperty("check")) {
            project.apply from: "$rootDir/.ops/ktlint.gradle"
        }
    }
}

task clean(type: Delete) {
    delete(rootProject.buildDir)
}

println("Version ${git.versionName()}")